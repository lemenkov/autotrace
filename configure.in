# configure.in for frontline
#
# Author: Masatake YAMATO (yamato@redhat.com)
#
AC_INIT(fit.c)

AM_INIT_AUTOMAKE(autotrace, "0.27pre")

AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB

dnl
dnl ImageMagick
dnl 
HAVE_MAGICK=no
AC_CHECK_PROG(MAGICK_CONFIG, Magick-config, Magick-config, "")
if test "x$MAGICK_CONFIG" = "xMagick-config" ; then
	CPPFLAGS_BACKUP="$CPPFLAGS"
	CPPFLAGS=`Magick-config --cppflags`
	AC_CHECK_HEADER(magick/api.h,
			magick_header_found=yes,
			magick_header_found=no)
	MAGICK_CFLAGS=${CPPFLAGS}
	CPPFLAGS=${CPPFLAGS_BACKUP}

	if test "x${magick_header_found}" = xyes ; then
		MAGICK_LDFLAGS="`Magick-config --ldflags` `Magick-config --libs`"
		AC_SUBST(MAGICK_CFLAGS)
		AC_SUBST(MAGICK_LDFLAGS)
		HAVE_MAGICK=yes
		AC_DEFINE(HAVE_MAGICK)
	else
		MAGICK_CFLAGS=
		AC_MSG_WARN([Magick-config is found but magick/api.h is not found in $MAGICK_CFLAGS])
 		AC_MSG_WARN([Check Magick-config.])
	fi
fi
AM_CONDITIONAL(HAVE_MAGICK, test x$HAVE_MAGICK = xyes)

dnl
dnl LIBPNG
dnl
HAVE_LIBPNG_STAGE1=no
AC_CHECK_LIB(png, png_get_image_width,
	HAVE_LIBPNG_STAGE1=yes ,,-lz)

HAVE_LIBPNG=no

if test "x$HAVE_LIBPNG_STAGE1" = "xyes" ; then
	AC_CHECK_LIB(png, png_read_png,
		HAVE_LIBPNG=yes ,,-lz)
	if test "x$HAVE_LIBPNG" = "xyes" ; then
		LIBPNG_LDFLAGS="-lpng -lz"
		AC_SUBST(LIBPNG_LDFLAGS)
		AC_DEFINE(HAVE_LIBPNG)
	else
	   AC_MSG_WARN([libpng exists but cannot find png_read_png in the libpng.])
	   AC_MSG_WARN([libpng might be old.])
	fi
fi
AM_CONDITIONAL(HAVE_LIBPNG, test x$HAVE_LIBPNG = xyes)

dnl
dnl LIBMING(LIBSWF)
dnl
HAVE_LIBSWF=no

AC_CHECK_HEADER(ming.h,
	        swf_header_found=yes,
		swf_header_found=no)
if test "${swf_header_found}" = yes ; then
	AC_CHECK_LIB(ming, Ming_init, 
		     HAVE_LIBSWF=yes,,-lm)
	if test "$HAVE_LIBSWF" = "yes" ; then
		LIBSWF_LDFLAGS="-lming"
		AC_SUBST(LIBSWF_LDFLAGS)
		AC_DEFINE(HAVE_LIBSWF)
	else
 		AC_MSG_WARN([Cannot find libming.])
		AC_MSG_WARN([SWF output is disabled.])
	fi
else
	AC_MSG_WARN([Cannot find ming.h.])
	AC_MSG_WARN([SWF output is disabled.])
fi
AM_CONDITIONAL(HAVE_LIBSWF, test x$HAVE_LIBSWF = xyes)

dnl
dnl Vender's cc(not gcc)
dnl
dnl AC_CANONICAL_HOST
dnl if test "x$CC" = xcc ; then
dnl   case host in
dnl 	mips-sgi-irix*)
dnl # Peter Cucka <pcucka at anim.dreamworks.com> wrote:
dnl # > Should I use -Ofast instead of -O2?
dnl # -Ofast selects optimizations that maximize speed, at the expense of
dnl # size, compile time, and, in some cases, floating point accuracy.
dnl # Compiled with -Ofast, AutoTrace runs about twice as fast as with -O2,
dnl # or even -O3, on the two machines (admittedly not a very extensive
dnl # sample) on which I've tested it.  I'd recommend using -Ofast, but
dnl # it's not necessary.
dnl            CFLAGS=-Ofast
dnl            ;;
dnl 	*)
dnl	    ;;
dnl  esac
dnl fi

AC_OUTPUT([Makefile autotrace-config], [chmod a+x autotrace-config])
